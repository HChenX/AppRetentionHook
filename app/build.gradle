import java.util.stream.Collectors

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)

    def keyKeyStoreFile = file(properties.getProperty('keyStoreFile'))
    def keyKeyStorePassword = properties.getProperty('keyStorePassword')
    def keyKeyAlias = properties.getProperty('keyAlias')
    def keyKeyAliasPassword = properties.getProperty('keyAliasPassword')


    signingConfigs {
        release {
            storeFile keyKeyStoreFile
            storePassword keyKeyStorePassword
            keyAlias keyKeyAlias
            keyPassword keyKeyAliasPassword
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }
    }

    namespace 'com.hchen.appretention'
    compileSdk 34

    defaultConfig {
        applicationId "com.hchen.appretention"
        minSdk 30
        targetSdk 34
        //noinspection HighAppVersionCode
        versionCode 2024111600
        versionName "5.0.2"
        ndk {
            // noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
        // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/**"
            excludes += "/kotlin/**"
            excludes += "/*.txt"
            excludes += "/*.bin"
            excludes += "/*.json"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21.toString()
    }

    buildFeatures {
        buildConfig true
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.register('runProcessTasks', JavaExec) {
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    mainClass = android.sourceSets.main.java.sourceFiles.getFiles().find {
        it.name.contains("ProcessTasks.java")
    }.toString()

    args = [android.sourceSets.main.java.sourceFiles.getFiles().stream().map {
        it.toString()
    }.collect(Collectors.toCollection { new ArrayList<String>() })]

    jvmArgs = ['-Xmx2024m', '-Dfile.encoding=UTF-8', '-Dstdout.encoding=UTF-8', '-Dstderr.encoding=UTF-8']
}

afterEvaluate {
    tasks.forEach {
        def name = it.getName()
        if (name.contains("JavaWithJavac") && !name.contains("Test")) {
            it.dependsOn(runProcessTasks)
        }
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    implementation 'org.luckypray:dexkit:2.0.2'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
    implementation(project(':HookTool'))
}
